apply plugin: 'com.android.library'

android {
    compileSdkVersion 26



    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 7
        versionName "0.1.6"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:26.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

//    // for bintray library, this didn't work here so put these in the project level Build.gradle file
//    classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
//    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
}

// for bintray library
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'



ext {
    bintrayRepo = 'maven'
    bintrayName = 'gnarlydialog'

    publishedGroupId = 'me.seandillon'
    libraryName = 'GnarlyDialog'
    artifact = 'gnarlydialog'

    libraryDescription = 'This is a fancy Android dialog library'

    siteUrl = 'https://github.com/sdillon1/GnarlyDialog'
    gitUrl = 'https://github.com/sdillon1/GnarlyDialog.git'

    libraryVersion = '0.1.6'

    developerId = 'me.seandillon'
    developerName = 'Sean Dillon'
    developerEmail = 'seandillonapps@gmail.com'

    licenseName = 'MIT'
    licenseUrl = 'https://github.com/sdillon1/GnarlyDialog/blob/master/LICENSE'
    allLicenses = ["MIT"]
}






// the below is for publishing the library to Bintray
// TODO move this below to a separate gradle file

group = publishedGroupId
version = libraryVersion

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId publishedGroupId
            artifactId artifact

            name libraryName
            description libraryDescription
            url siteUrl

            licenses {
                license {
                    name licenseName
                    url licenseUrl
                }
            }
            developers {
                developer {
                    id developerId
                    name developerName
                    email developerEmail
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
//    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        dryRun = false
        publish = true
        override = false
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
        }
    }
}

